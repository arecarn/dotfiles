" RYAN CARNEY VIMRC                                                        {{{
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
set cpo&vim
set fileformats=unix,dos

let mapleader="\<Space>"

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""}}}
" OPERATING SYSTEM VARIABLES                                               {{{
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
let s:hasWindows = has("win16") || has("win32") || has("win64")
if s:hasWindows
    let s:vimpath = $HOME."/vimfiles"
    cd $HOME "start in home directory, this is just here for convince
else
    let s:vimpath = $HOME."/.vim"
endif


"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""}}}
" PLUGINS                                                                  {{{
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" neobundle {{{2
if has('vim_starting')
    let s:runtimepath = s:vimpath.'/bundle/neobundle.vim/'
    execute 'set runtimepath+='.s:runtimepath
endif

call neobundle#begin(expand(s:vimpath.'/bundle'))
" Let NeoBundle manage NeoBundle
NeoBundleFetch 'Shougo/neobundle.vim' "}}}2

" vimproc.vim {{{2
NeoBundle 'Shougo/vimproc.vim', {
            \ 'build' : {
            \     'windows' : 'make -f make_mingw32.mak',
            \     'cygwin' : 'make -f make_cygwin.mak',
            \     'mac' : 'make -f make_mac.mak',
            \     'unix' : 'make -f make_unix.mak',
            \    },
            \ } "}}}2

" text objects {{{2
NeoBundle 'kana/vim-textobj-user'
NeoBundle 'saihoooooooo/vim-textobj-space'
NeoBundle 'Julian/vim-textobj-variable-segment'
NeoBundle 'coderifous/textobj-word-column.vim'
NeoBundle 'kana/vim-textobj-entire'
NeoBundle 'kana/vim-textobj-indent'
NeoBundle 'kana/vim-textobj-line'
NeoBundle 'mattn/vim-textobj-url'
NeoBundle 'glts/vim-textobj-comment'
NeoBundle 'thinca/vim-textobj-between'
NeoBundle 'osyo-manga/vim-textobj-multiblock' "}}}2

" unite {{{2
NeoBundle 'Shougo/unite.vim'
NeoBundle 'Shougo/unite-ssh'
NeoBundle 'Shougo/unite-outline'
NeoBundle 'Shougo/unite-session'
NeoBundle 'Shougo/neomru.vim'
NeoBundle 'tsukkee/unite-help'
NeoBundle 'ujihisa/unite-colorscheme'
NeoBundle 'kmnk/unite-command'
NeoBundle 'majkinetor/unite-cmdmatch'
NeoBundle 'voi/unite-ctags'
NeoBundle 'tsukkee/unite-tag'
NeoBundle 'thinca/vim-unite-history'
NeoBundle 'kopischke/unite-spell-suggest'
NeoBundle 'kmnk/vim-unite-giti'
NeoBundle 'hewes/unite-gtags' "}}}2

" operators {{{2
NeoBundle 'rhysd/vim-operator-surround' , {'depends' : 'kana/vim-operator-user'}
" NeoBundle 'tpope/vim-surround'
NeoBundle 'tommcdo/vim-exchange'
NeoBundle 'arecarn/crunch.vim', {'depends' : 'arecarn/selection.vim'}
NeoBundle 'tpope/vim-commentary'
NeoBundle 'tpope/vim-repeat' "}}}2

" NeoBundle 'Shougo/neocomplcache'
NeoBundle 'AndrewRadev/linediff.vim'
NeoBundle 'Lokaltog/vim-easymotion'
NeoBundle 'Shougo/echodoc.vim'
NeoBundle 'Shougo/neocomplete'
NeoBundle 'Shougo/neosnippet-snippets'
NeoBundle 'Shougo/neosnippet.vim'
NeoBundle 'Shougo/vimfiler.vim', {'depends' : 'Shougo/unite.vim'}
NeoBundle 'Shougo/vimshell.vim' "depends on 'Shougo/vimproc'
NeoBundle 'TeTrIs.vim'
NeoBundle 'altercation/vim-colors-solarized'
NeoBundle 'arecarn/Preserve.vim' "TODO Merge into the true one
NeoBundle 'arecarn/auto-file-loader.vim'
NeoBundle 'arecarn/clean-fold.vim'
NeoBundle 'arecarn/frisk.vim'
NeoBundle 'arecarn/git-diff-cached-preview.vim'
NeoBundle 'arecarn/go-to-buffer.vim'
NeoBundle 'arecarn/mixed-case-operator.vim'
NeoBundle 'arecarn/spell-utils.vim'
NeoBundle 'arecarn/templates'
NeoBundle 'bling/vim-airline'
NeoBundle 'chreekat/vim-paren-crosshairs'
NeoBundle 'dbakker/vim-lint'
NeoBundle 'dhruvasagar/vim-table-mode'
NeoBundle 'edkolev/tmuxline.vim'
NeoBundle 'godlygeek/tabular'
NeoBundle 'gregsexton/gitv'
NeoBundle 'henrik/vim-qargs'
NeoBundle 'hrsh7th/vim-neco-calc'
NeoBundle 'idanarye/vim-vebugger' "depends on 'Shougo/vimproc'
NeoBundle 'int3/vim-extradite'
NeoBundle 'junegunn/goyo.vim'
NeoBundle 'junegunn/seoul256.vim'
NeoBundle 'junegunn/vader.vim'
NeoBundle 'junegunn/vim-oblique' , {'depends' : 'junegunn/vim-pseudocl' }
NeoBundle 'justinmk/vim-sneak'
NeoBundle 'kana/vim-niceblock'
NeoBundle 'lopter/moin.vim'
NeoBundle 'mbbill/undotree'
NeoBundle 'morhetz/gruvbox'
NeoBundle 'nathanaelkane/vim-indent-guides'
NeoBundle 'scrooloose/syntastic'
NeoBundle 't9md/vim-choosewin'
NeoBundle 't9md/vim-quickhl'
NeoBundle 't9md/vim-textmanip'
NeoBundle 'talek/obvious-resize'
NeoBundle 'thinca/vim-fontzoom'
NeoBundle 'thinca/vim-qfreplace'
NeoBundle 'tommcdo/vim-lion'
NeoBundle 'tpope/vim-abolish'
NeoBundle 'tpope/vim-dispatch'
NeoBundle 'tpope/vim-eunuch'
NeoBundle 'tpope/vim-fugitive'
NeoBundle 'tpope/vim-markdown'
NeoBundle 'tpope/vim-scriptease'
NeoBundle 'tpope/vim-speeddating'
NeoBundle 'tpope/vim-unimpaired'
NeoBundle 'vim-scripts/DrawIt'
NeoBundle 'vim-scripts/FormatToWidth', {'depends' : 'vim-scripts/ingo-library'}
NeoBundle 'vim-scripts/UnconditionalPaste'
NeoBundle 'vim-scripts/cmdalias.vim'
NeoBundle 'vim-scripts/scons.vim'
NeoBundle 'w0ng/vim-hybrid'


call neobundle#end()
NeoBundleCheck

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""}}}
" PLUGINS CONFIG                                                           {{{
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
" netrw {{{2
" set up line numebring and other good stuff
let g:netrw_bufsettings = 'noma nomod rnu nobl nowrap ro'
" hide .swp .o12 .dbg .lst files in explorer
let g:netrw_list_hide= '.*\.swp$,.*\.o12$,.*\.dbg$,.*\.lst'
" netrw }}}2

if neobundle#is_installed('neocomplcache') "{{{2
    if !isdirectory(s:vimpath.'/utils_local/neocomplcache')
        call mkdir(s:vimpath.'/utils_local/neocomplcache', "p", 0700)
    endif
    let g:neocomplcache_temporary_dir = s:vimpath.'/utils_local/neocomplcache'
    let g:neocomplcache_enable_at_startup = 1
    let g:neocomplcache_enable_smart_case = 1
    let g:neocomplcache_enable_camel_case_completion = 1
    let g:neocomplcache_enable_underbar_completion = 1
    let g:neocomplcache_min_syntax_length = 2
    let g:neocomplcache_lock_buffer_name_pattern = '\*ku\*'
    let g:neocomplcache_enable_auto_delimiter = 1
    let g:neocomplcache_max_list = 100

    let g:neocomplcache_dictionary_filetype_lists = {
                \ 'default' : '',
                \ 'vimshell' : s:vimpath.'/.vimshell_hist',
                \ 'scheme' : s:vimpath.'/.gosh_completions'
                \ }

    " Define keyword, for minor languages
    if !exists('g:neocomplcache_keyword_patterns')
        let g:neocomplcache_keyword_patterns = {}
    endif
    let g:neocomplcache_keyword_patterns['default'] = '\h\w*'

    " imap <C-l>  <Plug>(neocomplcache_start_unite_complete)
    " imap <C-q>  <Plug>(neocomplcache_start_unite_quick_match)

    " Recommended key-mappings.
    " <CR>: close popup and save indent.
    inoremap <silent> <CR> <C-r>=<SID>my_cr_function()<CR>
    function! s:my_cr_function()
        " return neocomplcache#smart_close_popup() . "\<CR>"
        return pumvisible() ? neocomplcache#close_popup() : "\<CR>"
    endfunction
    " <TAB>: completion.
    inoremap <expr><TAB>  pumvisible() ? "\<C-n>" : "\<TAB>"

    " Enable heavy omni completion.
    " if !exists('g:neocomplcache_omni_patterns')
    "     let g:neocomplcache_omni_patterns = {}
    " endif
    " let g:neocomplcache_omni_patterns.php = '[^. \t]->\h\w*\|\h\w*::'
    " let g:neocomplcache_omni_patterns.perl = '\h\w*->\h\w*\|\h\w*::'
    " let g:neocomplcache_omni_patterns.c = '[^.[:digit:] *\t]\%(\.\|->\)'
    " let g:neocomplcache_omni_patterns.cpp = '[^.[:digit:] *\t]\%(\.\|->\)\|\h\w*::'
    " let g:neocomplcache_omni_patterns.ruby = '[^. *\t]\.\h\w*\|\h\w*::'
    " nnoremap conc :NeoComplCacheToggle<CR>
endif "}}}2

if neobundle#is_installed('neocomplete') "{{{2
    " Use neocomplete.
    let g:neocomplete#enable_at_startup = 1

    augroup NEOCOMPLETE
        autocmd!
        " autocmd BufRead * NeoCompleteLock
        autocmd FileType gitcommit let g:neocomplete#enable_auto_close_preview = 0
    augroup END
    nnoremap conc :NeoCompleteToggle<CR>

    let g:neocomplete#disable_auto_complete = 0
    let g:neocomplete#enable_smart_case = 1
    let g:neocomplete#enable_camel_case = 1
    let g:neocomplete#enable_under_bar = 1
    let g:neocomplete#enable_auto_delimiter = 1
    let g:neocomplete#sources#syntax#min_keyword_length = 4
    let g:neocomplete#sources#tags#cache_limit_size = 5000000000000000.0
    let g:neocomplete#max_list = 20

    if !isdirectory(s:vimpath.'/utils_local/neocomplete')
        call mkdir(s:vimpath.'/utils_local/neocomplete', "p", 0700)
    endif
    let g:neocomplete#data_directory = s:vimpath.'/utils_local/neocomplete/'

    " Define dictionary.
    let g:neocomplete#sources#dictionary#dictionaries = {
                \ 'default' : '',
                \ 'vimshell' : $HOME.'/.vimshell_hist',
                \ 'scheme' : $HOME.'/.gosh_completions'
                \ }

    " Define keyword.
    if !exists('g:neocomplete#keyword_patterns')
        let g:neocomplete#keyword_patterns = {}
    endif
    let g:neocomplete#keyword_patterns['default'] = '\h\w*'

    " Plugin key-mappings.
    inoremap <expr><C-g>     neocomplete#undo_completion()
    inoremap <expr><C-l>     neocomplete#complete_common_string()

    " Recommended key-mappings.
    " <CR>: close popup and save indent.
    inoremap <silent> <CR> <C-r>=<SID>my_cr_function()<CR>
    function! s:my_cr_function()
        return neocomplete#close_popup() . "\<CR>"
    endfunction

    " inoremap <expr><TAB>  pumvisible() ? "\<C-n>" : "\<TAB>"
    inoremap <expr><CR>  pumvisible() ? "\<Esc>" : "\<CR>"
    inoremap <expr><C-l> neocomplete#complete_common_string()

    " For smart TAB completion.
    inoremap <expr><TAB>  pumvisible() ? "\<C-n>" :
                \ <SID>check_back_space() ? "\<TAB>" :
                \ neocomplete#start_manual_complete()
    function! s:check_back_space() "{{{
        let col = col('.') - 1
        return !col || getline('.')[col - 1]  =~ '\s'
    endfunction

    inoremap <expr><BS> neocomplete#smart_close_popup()."\<C-h>"
    inoremap <expr><C-y>  neocomplete#close_popup()
    inoremap <expr><C-e>  neocomplete#cancel_popup()

    " Enable omni completion.
    autocmd FileType css setlocal omnifunc=csscomplete#CompleteCSS
    autocmd FileType html,markdown setlocal omnifunc=htmlcomplete#CompleteTags
    autocmd FileType javascript setlocal omnifunc=javascriptcomplete#CompleteJS
    autocmd FileType python setlocal omnifunc=pythoncomplete#Complete
    autocmd FileType xml setlocal omnifunc=xmlcomplete#CompleteTags

    " Enable heavy omni completion.
    if !exists('g:neocomplete#sources#omni#input_patterns')
        let g:neocomplete#sources#omni#input_patterns = {}
    endif
    let g:neocomplete#sources#omni#input_patterns.php = '[^. \t]->\h\w*\|\h\w*::'
    let g:neocomplete#sources#omni#input_patterns.c = '[^.[:digit:] *\t]\%(\.\|->\)'
    let g:neocomplete#sources#omni#input_patterns.cpp = '[^.[:digit:] *\t]\%(\.\|->\)\|\h\w*::'

    " For perlomni.vim setting.
    " https://github.com/c9s/perlomni.vim
    let g:neocomplete#sources#omni#input_patterns.perl = '\h\w*->\h\w*\|\h\w*::'
endif "}}}2

if neobundle#is_installed('neosnippet-snippets') "{{{2
    " imap <expr><TAB> neosnippet#expandable_or_jumpable() ?
    "             \ "\<Plug>(neosnippet_expand_or_jump)"
    "             \: pumvisible() ? "\<C-n>" : "\<TAB>"

    " inoremap <expr><TAB>  pumvisible() ? "\<C-n>" :
    "         \ <SID>check_back_space() ? "\<TAB>" :
    "         \ neocomplete#start_manual_complete()
    "   function! s:check_back_space() "{{{
    "     let col = col('.') - 1
    "     return !col || getline('.')[col - 1]  =~ '\s'
    "   endfunction"}}}

    imap <expr><TAB> neosnippet#expandable_or_jumpable() ?
                \ "\<Plug>(neosnippet_expand_or_jump)":
                \  pumvisible() ? "\<C-n>" :
                \ <SID>check_back_space() ? "\<TAB>" :
                \ neocomplete#start_manual_complete()
    function! s:check_back_space() "{{{
        let col = col('.') - 1
        return !col || getline('.')[col - 1]  =~ '\s'
    endfunction

    smap <expr><TAB> neosnippet#expandable_or_jumpable() ?
                \ "\<Plug>(neosnippet_expand_or_jump)"
                \: "\<TAB>"

    if has('conceal')
        set conceallevel=2 concealcursor=i
    endif

    let g:neosnippet#snippets_directory=s:vimpath.'/utils/snippets'
endif "}}}2

if neobundle#is_installed('unite.vim') "{{{2
    let g:unite_data_directory = s:vimpath.'/utils_local/unite/'
    let g:unite_winheight = 10
    let g:unite_source_history_yank_enable = 1
    let g:unite_source_history_yank_limit = 100
    let g:unite_split_rule = 'botright'
    call unite#filters#matcher_default#use(['matcher_fuzzy'])
    call unite#filters#sorter_default#use(['sorter_rank'])
    call unite#custom#source('file_rec,file_rec/async', 'max_candidates', 0)
    call unite#custom#profile('files', 'filters', 'sorter_rank')
    call unite#custom#source('file_rec,file_rec/async,file_mru,file,grep',
                \ 'ignore_pattern', join([
                \ '\.git',  '\.Trash', '\.ini$' ,'\.dbx$', '\.cache$'
                \ ], '\|'))
    " For ack.
    if executable('ack')
        let g:unite_source_grep_command = 'ack'
        let g:unite_source_grep_default_opts = '--no-heading --no-color -k -H'
        let g:unite_source_grep_recursive_opt = ''
    endif
    autocmd FileType unite call s:unite_settings()
    function! s:unite_settings()
        imap <Silent><Buffer><Expr> <C-s> unite#do_action('split')
        imap <Silent><Buffer><Expr> <C-v> unite#do_action('vsplit')
        imap <Silent><Buffer><Expr> <C-t> unite#do_action('tabopen')
        imap <Silent><Buffer><Expr> <C-g> unite#do_action('goto')
    endfunction
    let g:unite_source_menu_menus = {}
    let g:unite_source_menu_menus.interpreters = {
                \     'description' : 'Interpreter Menu',
                \ }
    let g:unite_source_menu_menus.interpreters.command_candidates = [
                \   ['VimShell', 'VimShell -split'],
                \   ['bc', 'VimShellInteractive bc'],
                \   ['octave', 'VimShellInteractive octave'],
                \   ['python', 'VimShellInteractive python'],
                \   ['ipython', 'VimShellInteractive ipython --colors=NoColor'],
                \ ]

    nnoremap <TAB><TAB>  :<C-u>Unite -start-insert<CR>
    xnoremap <TAB>g  y:<C-u>Unite -no-empty -no-resize -no-quit grep:.<CR><C-r>=escape(expand(@@), ' ')<CR><CR>
    nnoremap <TAB>g  :<C-u>Unite -no-empty -no-resize -no-quit grep:.<CR>
    nnoremap <TAB>gw :<C-u>UniteWithCursorWord -no-empty -no-resize -no-quit grep<CR>
    nnoremap <TAB>f  :<C-u>Unite file file_rec/async -start-insert -input=<CR>
    nnoremap <TAB>fg  :<C-u>Unite file file_rec/git -start-insert -input=<CR>
    nnoremap <TAB>r  :<C-u>Unite file_mru -start-insert<CR>
    nnoremap <TAB>rg :<C-u>Unite register -vertical<CR>
    nnoremap <TAB>b  :<C-u>Unite buffer<CR>
    nnoremap <TAB>o  :<C-u>Unite outline line -vertical<CR>
    nnoremap <TAB>l  :<C-u>Unite line -start-insert<CR>
    nnoremap <TAB>h  :<C-u>Unite help -start-insert<CR>
    nnoremap <TAB>s  :<C-u>Unite session -auto-resize<CR>
    nnoremap <TAB>ss :<C-u>UniteSessionSave<Space>
    nnoremap <TAB>i  :<C-u>Unite menu:interpreters -auto-resize<CR>
    nnoremap <TAB>hc :<C-u>Unite cmdmatch history/command<CR>
    nnoremap <TAB>ms :<C-u>Unite output:message<CR>
    nnoremap <TAB>hs :<C-u>Unite history/search<CR>
    nnoremap <TAB>p  :<C-u>Unite history/yank<CR>
    xnoremap <TAB>p  dk:<C-u>Unite history/yank<CR>
    nnoremap <TAB>ps  :<C-u>Unite process -start-insert<CR>
    nnoremap <TAB>ps  :<C-u>Unite process -start-insert<CR>
    nnoremap <TAB>up  :<C-u>Unite neobundle/update<CR>
    " nnoremap <TAB>t :<C-u>Unite gtags/<Tab>
    nnoremap <TAB>t :<C-u>UniteWithCursorWord tag<CR>

    " toggle the preview window when I press the "p" key.
    autocmd FileType unite call s:unite_my_settings()
    function! s:unite_my_settings()
        nnoremap <silent><buffer><expr> p
                    \ empty(filter(range(1, winnr('$')),
                    \ 'getwinvar(v:val, "&previewwindow") != 0')) ?
                    \ unite#do_action('preview') : ":\<C-u>pclose!\<CR>"
    endfunction
    let g:neocomplcache_tags_caching_limit_file_size = 5000000000000000.0
endif "}}}2

if neobundle#is_installed('vimfiler.vim') "{{{2
    " Replace netrw commands with vimfiler
    let g:loaded_netrw = 1
    let g:loaded_netrwPlugin = 1
    let g:vimfiler_as_default_explorer = 1

    command! -nargs=* Explore :VimFilerBufferDir <Args>
    command! -nargs=* Sexplore :VimFilerBufferDir -split -horizontal <Args>
    command! -nargs=* Vexplore :VimFilerBufferDir -split  <Args>
    command! -nargs=* Texplore :VimFilerBufferDir -tab <Args>
endif "}}}2

if neobundle#is_installed('vimshell.vim') "{{{2
    nnoremap <TAB>sh :VimShell -popup -toggle<CR><ESC>

    let g:vimshell_enable_start_insert = 1
    let g:vimshell_vimshrc_path = s:vimpath.'/utils/vimshell/vimshellrc'

    if !isdirectory(s:vimpath.'/utils_local/vimshell')
        call mkdir(s:vimpath.'/utils_local/vimshell', "p", 0700)
    endif
    let g:vimshell_data_directory = s:vimpath.'/utils_local/vimshell/'
endif "}}}2

if neobundle#is_installed('echodoc.vim') "{{{2
    let g:echodoc_enable_at_startup=1
    set cmdheight=2
endif "}}}2

if neobundle#is_installed('vim-oblique') "{{{2
    let g:oblique#incsearch_highlight_all = 1
endif "}}}2

if neobundle#is_installed('vim-fugitive') "{{{2
    nnoremap <Leader>gs :Gstatus<CR>
    nnoremap <Leader>gd :Gdiff<CR>
    nnoremap <Leader>gc :Gcommit<CR>
    nmap <Leader>gj <C-W>P<C-N>D
    nmap <Leader>gk <C-W>P<C-P>D
endif "}}}2

if neobundle#is_installed('frisk.vim') "{{{2
    call frisk#AddEngine('yahoo', 'http://search.yahoo.com/search?p=')

    command! Rfrisk call ReloadFrisk()
    "Do the stuff for frisk
    function! ReloadFrisk()
        Runtime ~/Dropbox/projects/frisk/gc/frisk/plugin/*.vim
        Runtime ~/Dropbox/projects/frisk/gc/frisk/autoload/*.vim
        Runtime ~/Dropbox/projects/frisk/gc/frisk/autoload/frisk/*.vim
    endfunction
endif "}}}2

if neobundle#is_installed('tabular') "{{{2
    nnoremap <Leader>a :Tabularize/
    xnoremap <Leader>a :Tabularize/
endif "}}}2

if neobundle#is_installed('vim-indent-guides') "{{{2
    nmap <silent> coig <Plug>IndentGuidesToggle
    let g:indent_guides_enable_on_vim_startup = 0
    let g:indent_guides_guide_size = 1
endif "}}}2

if neobundle#is_installed('syntastic') "{{{2
    let g:syntastic_check_on_open = 0
    let g:syntastic_python_checkers = ['python']
    nnoremap cost :SyntasticToggleMode<CR>
endif "}}}2

if neobundle#is_installed('vim-textmanip') "{{{2
    xmap <C-j> <Plug>(textmanip-move-down)
    xmap <C-k> <Plug>(textmanip-move-up)
    xmap <C-h> <Plug>(textmanip-move-left)
    xmap <C-l> <Plug>(textmanip-move-right)

    " toggle insert/replace with <F10>
    nmap comt <Plug>(textmanip-toggle-mode)
endif "}}}2

if neobundle#is_installed('vim-quickhl') "{{{2
    nmap m <Plug>(quickhl-manual-this)
    nnoremap gm m
    nnoremap gm m
    xmap m <Plug>(quickhl-manual-this)
    nmap M <Plug>(quickhl-manual-reset)
    xmap M <Plug>(quickhl-manual-reset)
endif "}}}2

if neobundle#is_installed('vim-easymotion') "{{{2
    map <Nop> <Plug>(easymotion-prefix)
    map e/ <Plug>(easymotion-sn)
    map e? <Plug>(easymotion-sn)
endif "}}}2

if neobundle#is_installed('vim-choosewin') "{{{2
    nmap <C-w>? <Plug>(choosewin)
    let g:choosewin_overlay_enable = 1
    let g:choosewin_overlay_clear_multibyte = 1
    let g:choosewin_statusline_replace = 0
endif "}}}2

if neobundle#is_installed('rainbow') "{{{2
    let g:rainbow_active = 1
endif "}}}2

if neobundle#is_installed('cmdalias.vim') "{{{2
    command! Pwf echo expand('%:p')
    augroup VIMRC_aliases
        au!
        au VimEnter * Alias pwf Pwf
        au VimEnter * Alias ex Explore
        au VimEnter * Alias sex Sexplore
        au VimEnter * Alias tex Texplore
        au VimEnter * Alias vex Vexplore
    augroup END
endif "}}}2

if neobundle#is_installed('FormatToWidth') "{{{2
    xmap gQ <Plug>FormatToWidth
endif "}}}2

if neobundle#is_installed('vim-fontzoom') "{{{2
    let g:fontzoom_no_default_key_mappings = 1
    nnoremap  ]z <Plug>(fontzoom-larger)
    nnoremap  [z <Plug>(fontzoom-smaller)
endif "}}}2

if neobundle#is_installed('linediff.vim') "{{{2
    xnoremap <Leader>ld :Linediff<CR>
endif "}}}2

if neobundle#is_installed('vim-easyclip') "{{{2
    let g:EasyClipUseSubstituteDefaults = 1
    let g:EasyClipEnableBlackHoleRedirect = 0
    let g:EasyClipUseCutDefaults = 0
endif "}}}2

if neobundle#is_installed('vim-textobj-between') "{{{2
    if neobundle#is_installed('vim-textobj-between')
        let g:textobj_between_no_default_key_mappings = 1
        xmap am <Plug>(textobj-between-a)
        omap am <Plug>(textobj-between-a)
        xmap im <Plug>(textobj-between-i)
        omap im <Plug>(textobj-between-i)
    endif
endif "}}}2

if neobundle#is_installed('vim-textobj-comment') "{{{2
    let g:textobj_comment_no_default_key_mappings = 1
    xmap a# <Plug>(textobj-comment-a)
    omap a# <Plug>(textobj-comment-a)
    xmap i# <Plug>(textobj-comment-i)
    omap i# <Plug>(textobj-comment-i)
endif "}}}2

if neobundle#is_installed('vim-airline') "{{{2
    let g:airline#extensions#tabline#enabled = 1
    let g:airline#extensions#tabline#tab_nr_type = 1
    let g:airline#extensions#tabline#buffer_nr_show = 1
    let g:airline_powerline_fonts = 1
    let g:airline_inactive_collapse = 0
    let g:airline_theme='gruvbox'
endif "}}}2

if neobundle#is_installed('vim-table-mode') "{{{2
    let g:table_mode_separator = '|'
endif "}}}2

if neobundle#is_installed('undotree') "{{{2
    nnoremap <TAB>ut :UndotreeToggle<CR>:UndotreeFocus<CR>
endif "}}}2

if neobundle#is_installed('goyo.vim') "{{{2
    nnoremap cof :<C-U>setlocal wrap<CR>:<C-U>call SetGoyo(v:count)<CR>
    function! SetGoyo(num)
        if a:num == 0
            Goyo
        else
            execute "Goyo ".string(a:num)
        endif
    endfunction
endif "}}}2

if neobundle#is_installed('scons.vim') "{{{2
    augroup SCONS
        autocmd!
        autocmd BufNewFile,BufRead SCons*,scons* set filetype=scons
    augroup END
endif "}}}2

if neobundle#is_installed('obvious-resize') "{{{2
    "map F9 - F12 (like hjkl) to resize windows
    nnoremap  <silent><F9>  :ObviousResizeLeft<CR>
    nnoremap  <silent><F10> :ObviousResizeDown<CR>
    nnoremap  <silent><F11> :ObviousResizeUp<CR>
    nnoremap  <silent><F12> :ObviousResizeRight<CR>
endif "}}}2

if neobundle#is_installed('Preserve.vim') "{{{2
    nnoremap <silent><leader>dt :PreserveSave<CR>:%s,\s\+$,,ge<CR>:PreserveRestore<CR>
    nnoremap =ae :call Preserve("normal! gg=G")<CR>
    nnoremap =ie :call Preserve("normal! gg=G")<CR>
    nnoremap yae :call Preserve("normal! ggyG")<CR>
    nnoremap yie :call Preserve("normal! ggyG")<CR>
endif "}}}2

if neobundle#is_installed('moin.vim') "{{{2
    augroup MOIN
        autocmd!
        autocmd BufRead,BufNewFile *.moin set filetype=moin
    augroup END
endif "}}}2

if neobundle#is_installed('vim-operator-surround') "{{{2
    map <silent>sa <Plug>(operator-surround-append)
    map <silent>sd <Plug>(operator-surround-delete)
    map <silent>sr <Plug>(operator-surround-replace)
endif "}}}2

if neobundle#is_installed('crunch.vim') "{{{2
    let g:crunch_user_variables = {'e': exp(1),  'pi':3.14159265359}

    command! Rcrunch call ReloadCrunch()
    " Do the stuff for crunch
    function! ReloadCrunch()
        Runtime ~/Dropbox/projects/crunch/crunch/plugin/*.vim
        Runtime ~/Dropbox/projects/crunch/crunch/autoload/*.vim
        Runtime ~/Dropbox/projects/crunch/crunch/autoload/crunch/*.vim
    endfunction

    " do crunch tests
    if s:hasWindows
        nnoremap <Leader>ct :!cd \Users\arecarn\Dropbox\repos\crunch\crunch\tests && gvim -u ..\.vimrc_minimal +Vader*<CR>
        nnoremap <Leader>tc :!cd \Users\arecarn\Dropbox\repos\crunch\crunch\tests && gvim -u ..\.vimrc_minimal<CR>
    else
        nnoremap <Leader>ct :!cd ~/Dropbox/repos/crunch/crunch/tests && /usr/local/bin/vim -u ../.vimrc_minimal +Vader\*<CR>
        nnoremap <Leader>tc :!cd ~/Dropbox/repos/crunch/crunch/tests && /usr/local/bin/vim -u ../.vimrc_minimal<CR>
    endif
endif "}}}2

if neobundle#is_installed('operator-surround') "{{{2
    map <silent>sa <Plug>(operator-surround-append)
    map <silent>sd <Plug>(operator-surround-delete)
    map <silent>sr <Plug>(operator-surround-replace)
endif "}}}2

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""}}}
" FILE TYPE SETTINGS                                                       {{{
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
filetype on
filetype plugin on
filetype indent on
syntax on " allow syntax highlighting
set encoding=utf-8 "make Vim use UTF-8 by default

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""}}}
" UI OPTIONS                                                               {{{
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
set colorcolumn=79
set number
set relativenumber
set showcmd
set list
set lazyredraw
set laststatus=2
set listchars=trail:·,tab:»·,extends:→,precedes:←

" Maximize Window on Startup
if has ("gui_running")
    if has("autocmd") && s:hasWindows
        augroup windowsgui:
            autocmd GUIEnter * simalt ~x " maximize in windows
        augroup END
    else
    endif
endif

" Highlight TODO, FIXME, NOTE, etc.
if has("autocmd")
    if v:version > 701
        autocmd Syntax * call matchadd('Todo',  '\v\W\zs<(TODO|FIXME|CHANGED|XXX|BUG|HACK|TRICKY)>')
        autocmd Syntax * call matchadd('Debug', '\v\W\zs<(NOTE|INFO|IDEA)>')
    endif
endif

" gui settings {{{2
if has ("gui_running")
    "Make GVim more like Terminal Vim
    set guioptions-=T "remove toolbar
    set guioptions-=r "remove right-hand scroll bar
    set guioptions-=l "remove right-hand scroll bar
    set guioptions=c "makes dialogs boxes appear like they do in console vim

    "Set Font
    if s:hasWindows
        set guifont=DejaVu_Sans_Mono_For_Powerline:h10:cANSI
        "set guifont=DejaVu_Sans_Mono:h10:cANSI,Lucida_Console:h9:cANSI,Consolas:h9:cANSI
    elseif has('mac')
        set guifont=DejaVu\ Sans\ Mono\ For\ Powerline:h11
        " set guifont=Monaco:h12
    elseif has('unix') && !has('mac') "linux
        set guifont=DejaVu\ Sans\ Mono\ 10
    endif
endif

colorscheme gruvbox
let g:gruvbox_italic=0
set background=dark

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""}}}
" CURSOR SETTINGS                                                          {{{
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
set scrolloff=6 " Keep 3 lines (top/bottom) for scope
set sidescrolloff=6 " keep 3 lines (side/side) for scope
set sidescroll=1
" In many terminal emulators the mouse works just fine, thus enable it.
if has('mouse')
    set mouse=a
endif

set ttyfast
set ttymouse=xterm2

set cursorline
if has("autocmd")
    augroup CURSOR
        autocmd WinEnter * setlocal cursorline
        autocmd WinLeave * setlocal nocursorline
    augroup END
endif


"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""}}}
" UNDO & SWAP                                                              {{{
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
set undofile
let s:undodir = s:vimpath.'/utils_local/undo//'
execute 'set undodir='.s:undodir
set undolevels=20000
set undoreload=20000


" let s:swap=s:vimpath.'/utils_local/swap//'
set noswapfile
" execute 'set directory='.s:swap
" ^ Here be magic! Quoth the help: (from godlygeek)
" For Unix and Win32, if a directory ends in two path separators "//" or "\\",
" the swap file name will be built from the complete path to the file with all
" path separators substituted to percent '%' signs.  This will ensure file
" name uniqueness in the preserve directory.
set history=10000
set backup
" Make the back up file extension year-month-day_hour ex: file.txt_13-02-24_15
let myvar = strftime("%y-%m-%d_%H")
let myvar = "set backupext=_". myvar
execute myvar
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""}}}
" BACKUP                                                                  {{{
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
let s:backupdir = s:vimpath.'/utils_local/backup//'
execute 'set backupdir='.s:backupdir

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""}}}
" LINE WRAPPING                                                            {{{
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
set nowrap
set linebreak
set textwidth=0 "TODO for c make tw=78

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""}}}
" SPELLING - THESAURUS                                                     {{{
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
set spell
set spelllang=en_us
let &spellfile=s:vimpath.'/utils/spell/spellfile.add'
let &thesaurus=s:vimpath.'/utils/thesaurus/thesaurus.txt'

nnoremap g<C-s> viw<Esc>a<C-X><C-S><C-p>

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""}}}
" TABS, INDENTING & FORMAT OPTIONS                                         {{{
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
set expandtab
set tabstop=4
set shiftwidth=4
set softtabstop=4
set smarttab

set autoindent
set formatoptions+=n "recognize ordered lists
set formatoptions+=j "remove comments leader when joining

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""}}}
" FOLDING                                                                  {{{
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
set foldtext=CleanFold('_')
set foldmethod=indent
set foldcolumn=2
set foldnestmax=1
set foldlevelstart=99

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""}}}
" AUTOCMDS                                                                 {{{
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
if has("autocmd")
    augroup vimrc
        autocmd!
        autocmd BufWritePost .vimrc,_vimrc,vimrc,.gvimrc,_gvimrc,gvimrc so $MYVIMRC
    augroup END

    augroup AllFiles
        autocmd!
        autocmd FocusLost * silent! wall
    augroup END

    augroup QuickFix
        autocmd!
        "opens quick fix window on the bottom of all screens
        autocmd FileType qf wincmd J

        " Automatically open, but do not go to (if there are errors) the quickfix /
        " location list window, or close it when is has become empty.
        "
        " Note: Must allow nesting of autocmds to enable any customizations for quickfix
        " buffers.
        " Note: Normally, :cwindow jumps to the quickfix window if the command opens it
        " (but not if it's already open). However, as part of the autocmd, this doesn't
        " seem to happen.
        autocmd QuickFixCmdPost [^l]* nested cwindow
        autocmd QuickFixCmdPost    l* nested lwindow
    augroup END

    if s:hasWindows
        augroup TEX
            autocmd!
            autocmd BufEnter *.tex setf tex
            "autocmd BufWritePost *.tex cd %:p:h | !start /min pdflatex %:t
            autocmd BufWritePost *.tex cd %:p:h | !start pdflatex %:t
            autocmd BufEnter *.tex setlocal concealcursor=
        augroup END
    else
        augroup TEX
            autocmd!
            autocmd BufWritePost *.tex cd %:p:h | !pdflatex %:t
            autocmd BufEnter *.tex setlocal concealcursor=
        augroup END
    endif
endif


set cscoperelative
set cscopetag

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""}}}
" SEARCHING                                                                {{{
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
set wrapscan
set incsearch
set ignorecase
set smartcase

set path=**,.,./**

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""}}}
" GENERAL BEHAVIOR                                                         {{{
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
set clipboard=unnamedplus,unnamed
set splitright

" disable dang error{visual}bells for good
if has("autocmd")
    augroup noBells
        autocmd!
        autocmd GUIEnter * set vb t_vb=
        autocmd VimEnter * set vb t_vb=
    augroup END
endif

set switchbuf=usetab
set shortmess+=a
set autowriteall
set autoread

set updatetime=5000  "milliseconds
set confirm
set backspace=indent,eol,start
set tags=tags;/ "the ";/" allows you to look back wards
set exrc
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""}}}
" COMMAND LINE COMPLETION                                                               {{{
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
set wildmode=longest,full
set wildmenu
set completeopt=menuone,menu,preview
set wildcharm=<TAB>
set wildignore=*.o,*.obj,*.pyc,*~,*.git,*.hg*,*.svn,*.DS_Store

"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""}}}
" MAPPINGS & COMMANDS                                                      {{{
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
set timeout
set timeoutlen=500
set updatetime=1000

set grepprg=ack\ -H\ --nocolor\ --nogroup

" Navigation
nnoremap <expr> k (v:count == 0 ? 'gk' : 'k')
nnoremap <expr> j (v:count == 0 ? 'gj' : 'j')
nnoremap gk k
nnoremap gj j

xnoremap gr r<space>gvo<esc>R

nnoremap <silent> g_ :execute "normal! yypVr".nr2char(getchar())<CR>

inoremap <C-F> <ESC>gwapa

nnoremap cotw :<C-U>setlocal textwidth=
nnoremap cocl :<C-U>setlocal conceallevel=

" update diff with a key mapping
nnoremap du :diffupdate<CR>

nnoremap <silent><C-l> :redraw!<CR><C-l>

nnoremap <TAB>rc :e $MYVIMRC<CR>

nnoremap <C-W>d :bd<CR>

" make more undoes possible after leaving insert mode
inoremap <C-u> <C-g>u<C-u>
inoremap <C-w> <C-g>u<C-w>
inoremap <BS> <C-g>u<BS>
inoremap <Del> <C-g>u<Del>
inoremap <END> <C-g>u<END>

noremap Y y$

" visual selection text object {{{3
xnoremap a/ gn
onoremap a/ gn
xnoremap i/ gn
onoremap i/ gn
xnoremap i? gN
onoremap i? gN
xnoremap a? gN
onoremap a? gN
"}}}3

nnoremap <leader>qa :qa!<Cr>
nnoremap <leader>q :q!<Cr>

nnoremap <Leader>yf :let @f=expand('%:p')<CR>:echo @f.' ---> yanked into register f'<CR>
nnoremap <Leader>yd :let @f=expand('%:h')<CR>:echo @f.' ---> yanked into register f'<CR>

" disable Ex mode key and map it to something awesome
nnoremap Q @q
" replay macro for each line of a visual selection
xnoremap Q :normal! @q<CR>
" repeat last command for each line of a visual selection
xnoremap . :normal! .<CR>
nnoremap <C-n> .n
nnoremap <C-p> .N

" navigation in command line
cnoremap <C-p> <UP>
cnoremap <C-n> <DOWN>
cnoremap <C-h> <LEFT>
cnoremap <C-l> <RIGHT>
cnoremap <C-a> <HOME>
cnoremap <C-e> <END>

cnoremap %e e **/*
cnoremap %f find<SPACE>*

" current file and directory abbreviations

" (requires "set wildcharm = <TAB>)
cnoremap %o <C-r>=expand('%:p:r:.')<CR><Tab><S-Tab>
cnoremap %f <C-r>=expand('%:p:.')<CR>
cnoremap %d <C-r>=expand('%:h:.')<CR>/


nnoremap <leader>eo :e <C-r>=expand('%:p:r:.')<CR><Tab><S-Tab>
nnoremap <leader>ef :e <C-r>=expand('%:p:.')<CR>
nnoremap <leader>ed :e <C-r>=expand('%:h:.')<CR>/

nnoremap <Leader>gcd :cd! %:h<Cr>:pwd<Cr>
nnoremap <Leader>cd :lcd! %:h<Cr>:pwd<Cr>

" accept completion (requires "set wild charm = <TAB>)
cnoremap <C-y> <SPACE><BS><TAB>

nnoremap S i<cr><esc>k$
nnoremap gJ ddpkJ


"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""}}}
" SCRIPTS                                                                  {{{
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
function! PrintCscopeOptions()
    echo "g: Definition"
    echo "c: Functions calling this function"
    echo "d: Functions called by this function"
    echo "f: This file"
    echo "i: Files #including this file"
    echo "s: This C symbol"
    echo "t: This text string"
    echo "e: This egrep pattern"
endfunction
execute "nnoremap <Leader>csf :call PrintCscopeOptions()<CR>:cs find "
execute "nnoremap <Leader>cs :call PrintCscopeOptions()<CR>:cs find   <C-r><C-w><HOME>".repeat('<Right>',8)

" TODO Is the word under the cursor a number?
function! DoNum(operator)
    let line = getline('.')
    let cIndex = getpos('.')[2] -1 "TODO handle virtual edit
    echom cIndex
    execute "let rLine = line[".cIndex.":]"
    echom "relavant line =".rLine
    " let rLine = substitute(rLine, '\(\d\+\|0x\x\+\)', '\=submatch(0)*10','')
    execute "let rLine = substitute(rLine, \"\(\d\+\|0x\x\+\)\", \"\=submatch(0)*10\",\"\")"
    echom rLine
    let cIndex -= 1
    execute "let line = line[:".cIndex."].rLine"
    echom line
    call setline('.', line)
endfunction

function! CalculateCursor(x, operator)
    let p = @/
    try
        silent exe "s%-\\?\d*\\%#\\d\\+%\\=submatch(0) " . a:operator . " a:x%"
        exe "normal \<C-O>"
    catch /^Vim\%((\a\+)\)\=:E486/
        try
            silent exe "normal /\\%#.\\{-}\\zs\\d\\+/b\<CR>"
            exe "s%-\\?\d*\\%#\\d\\+%\\=submatch(0) " . a:operator . " a:x%"
            exe "normal \<C-O>"
        catch /^Vim\%((\a\+)\)\=:E486/
        endtry
    finally
        let @/ = p
    endtry
endfunction

" vnoremap <silent><c-x> :<C-U>'<,'>call CalculateCursor(v:count1, "-")<CR>:noh<CR>gv
" vnoremap <silent><c-a> :<C-U>'<,'>call CalculateCursor(v:count1, "+")<CR>:noh<CR>gv
" noremap <silent>g<c-x> :<C-U>call CalculateCursor(v:count1, "*")<CR>:noh<CR>
" vnoremap <silent>g<c-x> :<C-U>'<,'>call CalculateCursor(v:count1, "*")<CR>:noh<CR>gv
" noremap <silent>g<c-a> :<C-U>call CalculateCursor(v:count1, "/")<CR>:noh<CR>
" vnoremap <silent>g<c-a> :<C-U>'<,'>call CalculateCursor(v:count1, "/")<CR>:noh<CR>gv

nnoremap <leader>m  :call SearchPosition()<CR>
nnoremap <leader>M  :let @/ = ""<CR>
function! SearchPosition()
    if @/ !~ '\v(\\\%\d+[lc](\\\|)?)+'
        let @/ = ''
        " echo "pattern didn't match = ".@/
    else
        let @/ .= '\|'
        " echo "added bar = ".@/
    endif

    let line = getpos('.')[1]
    let col = getpos('.')[2]
    execute "let @/ .= '\\%".line."l'"
    execute "let @/ .= '\\%".col."c'"
endfunction

xnoremap g* :<C-U>call AddToSearch()<CR>
function! AddToSearch()
    let save_reg = @@
    if @/ != ''
        let @/ .= '\|'
    endif
    normal! gvy
    let @/ .= @@
    let @@ = save_reg
endfunction


command!  PreviewTagToggle call PreviewWordToggle()
nnoremap <Leader>tp :ptjump <C-r><C-w><CR>


" Run in VimShell""""""""""""""""""""""""""""""""""""""""""""""""""""""""""{{{2
command! -nargs=* VshSend call RunInVimShell('<Args>')
function! RunInVimShell(cmd)
    VimShellBufferDir -popup
    execute "VimShellSendString " . a:cmd
endfunction
" Run in VimShell""""""""""""""""""""""""""""""""""""""""""""""""""""""""""}}}2

" add numbers to the front or end of a line
cabbrev numend s/$/\=1-line("'<")+line(".")/c
cabbrev numfront s/^/\=1-line("'<")+line(".")/c

" Details:
" s/\d\+/ : Match one or more digits (\d\+) on any line (:%) and substitute (s).
"            \= : for each match, replace with the result of the following expression:
" printf("0x%04x", : produce a string using the format "0x%04x", which corresponds to a literal 0x followed by a four digit (or more) hex number, padded with zeros.
" submatch(0) : The result of the complete match (i.e. the number).
cabbrev tohex s/\d\+/\=printf("0x%x", submatch(0))
" find the first occurrence of a hex digit and changes it to decimal
cabbrev todec s/0x\x\+/\=printf("%d", submatch(0))
cabbrev toasc s/0x\x\+/\=printf("%c", submatch(0))
" delete the current character and replace it with it's ASCII Decimal value
nnoremap <Leader>tad xi<C-r>=printf("%d",char2nr("<C-r>""))<CR><ESC>
" delete the current character and replace it with it's ASCII Hex value
nnoremap <Leader>tah xi<C-r>=printf("0x%x",char2nr("<C-r>""))<CR><ESC>
" noremap <Leader>cs  :s/./\=printf("Ox%x + ",char2nr("submatch(0)"))/gc<CR>


" reverse selection kind of works
vnoremap <silent> <leader>is :<c-u>let old_reg_a=@a<cr>
            \:let old_reg=@"<cr>
            \gv"ay
            \:let @a=substitute(@a, '.\(.*\)\@=',
            \ '\=@a[strlen(submatch(1))]', 'g')<CR>
            \gvc<C-R>a<Esc>
            \:let @a=old_reg_a<CR>
            \:let @"=old_reg<CR>


command! -nargs=* -range=0 -bang Seq
            \ call Seq(<count>, <line1>, <line2>, <q-args>, "<bang>")

function! Seq(count, firstLine, lastLine, cmdInput, bang) abort
    "anything within backticks
    let rg_expr = '`\(.\{-1,}\)`'

    " 1 index if ! is used
    if a:bang != '!' 
        let x = 0
    else
        let x = 1
    endif

    for i in range(0, a:lastLine-a:firstLine) "TODO fails without a range
        let line = substitute(a:cmdInput, rg_expr, '\=eval(submatch(1))', 'g')
        call append(i+a:firstLine, line)
        let x = x + 1
    endfor
endfunction

nnoremap <silent> <leader>S  :execute ':s#\V'.escape(nr2char(getchar()), '\').'#\r#g'<CR>
nnoremap <silent> <leader>gS :execute ':s#\s*\V'.escape(nr2char(getchar()), '\').'\v\s*#\r#g'<CR>
xnoremap <silent> <leader>J  :<C-U>execute '''<,''>:s#\n#'.escape(nr2char(getchar()), '\').'#g'<CR>
xnoremap <silent> <leader>gJ :<C-U>execute '''<,''>:s#\n#'.escape(nr2char(getchar()), '\').' #g'<CR>

command! -nargs=* -range=0 -bang Split
            \ call Split(<count>, <line1>, <line2>, <q-args>, '<bang>')
function! Split(count, first_line, last_line, input, bang) abort
    let input = a:input
    if input == ''
        let input = nr2char(getchar())
    endif

    let selection = selection#new(a:count, a:first_line, a:last_line)
    if selection.content == ''
        throw 'nothing there'
    endif

    let expr_list = split(selection.content, '\n', 1)

    for i in range(len(expr_list))
        let expr_list[i] = join(split(expr_list[i], input),"\n")
    endfor

    let expr_lines = join(expr_list, "\n")
    call selection.over_write(expr_lines)
endfunction


command! -nargs=* -range=0 -bang Join
            \ call Join(<count>, <line1>, <line2>, <q-args>, '<bang>')
function! Join(count, first_line, last_line, input, bang) abort
    let input = a:input
    if input == ''
        let input = nr2char(getchar())
    endif

    let selection = selection#new(a:count, a:first_line, a:last_line)
    if selection.content == ''
        throw 'nothing there'
    endif

    let expr_list = split(selection.content, '\n', 0)

    let expr_lines = join(expr_list, input)
    call selection.over_write(expr_lines)
endfunction


"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""}}}
" FIN                                                                      {{{
""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
try
    source ~/.vimrc_local
    source ./.vimrc_local
catch
endtry

" Refreshing airline
" TODO see why this has to be last
if !has('vim_starting') && g:loaded_airline
    AirlineRefresh
    normal! gtgT
endif "}}}2


set  modeline
" vim: textwidth=78
" vim: foldmethod=marker
"""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""}}}
